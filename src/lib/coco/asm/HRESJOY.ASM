00010 * TANDY TYPE HIGH RESOLUTION JOYSTICK ROUTINE
00020 * (c) NOV. 1992 BY ROBERT GAULT
00030 
00040 * READS RIGHT JOYSTICK AND STORES X & Y VALUES AT OFFSET
00050 * 0 & 2; ROUTINE STARTS AT 4
00060 
00070 * COCO3 FAST MODE ONLY; COMPENSATES FOR 320/640 FORMAT
00080 * SHOULD BE OFFSET LOADED TO DESIRED ADDRESS, PIC CODE
00090 
00100 	ORG	0
00110 XVAL	RMB	2
00120 YVAL	RMB	2
00130 START	PSHS	CC
00140 	SYNC		REDUCES JITTER
00150 	ORCC	#$50	TURN OFF INTERRUPTS
00160 	LDA	$FF23	SOUND CONTROL; SAVE AND TURN OFF
00170 	STA	<SOUND,PCR
00180 	ANDA	#.NOT.8		NO SOUND
00190 	STA	$FF23
00200 	LDA	$FF01	MUX VALUES; SAVE AND SET FOR X JSTICK
00210 	STA	<PIA0A,PCR
00220 	ANDA	#.NOT.8		MUX=0
00230 	STA	$FF01
00240 	LDA	$FF03
00250 	STA	<PIA0B,PCR
00260 	ANDA	#.NOT.8		MUX=0
00270 	STA	$FF03	MUX IS NOW 0,0
00280 	LDX	#$FF00		SET FOR INDIRECT INSTRUCTION
00290 	BSR	READJS	READ JOY STICK VALUE; X
00300 	STD	<XVAL,PCR	SAVE THE RAW ANSWER
00310 	LDA	<PIA0A,PCR	SET MUX FOR Y JSTICK
00320 	ORA	#8		MUX=1
00330 	STA	$FF01		MUX NOW IS 0,1
00340 	BSR	READJS	READ JOYSTICK; Y
00350 	STD	<YVAL,PCR	SAVE ANSWER
00360 	LDD	<PIA0A,PCR	RESTORE ORIGINAL MUX VALUES
00370 	STA	$FF01
00380 	STB	$FF03
00390 	LDA	<SOUND,PCR	RESTORE ORIGINAL SOUND VALUES
00400 	STA	$FF23
00410 	LDA	$E6	HRES GRAPHICS MODE INDICATOR
00420 	CMPA	#3	HAVE WE A 320 OR 640 SCREEN
00430 	BHS	E0
00440 	LDD	<XVAL,PCR	IF 320; DIVIDE X VALUE BY 2
00450 	LSRA
00460 	RORB
00470 	STD	<XVAL,PCR
00480 E0	PULS	CC,PC	RETURN TO CALLING PROGRAM
00490 
00500 SOUND	RMB	1
00510 PIA0A	RMB	1
00520 PIA0B	RMB	1
00530 
00540 READJS	LDD	#$FE8C		REG.A=DAC; REG.B=DISCHARGE COUNT
00550 	STA	$FF20		TURN OFF RAMP IF ON
00560 A0	DECB			WAIT FOR FULL DISCHARGE
00570 	BNE	A0
00580 	LDD	#$329		MAX COUNT
00590 	PSHS	A
00600 	LDA	#2		RAMP TRIGGER
00610 	STA	$FF20		START RAMP
00620 B0	LDA	,X		TEST DAC FOR MATCH
00630 	BMI	C0		EXIT IF MATCH
00640 	SUBB	#1		LSB COUNT
00650 	BHS	B0
00660 	DEC	,S		MSB COUNT
00670 	BPL	B0
00680 C0	LDA	#$FE
00690 	STA	$FF20		STOP RAMP
00700 	PULS	A
00710 	TSTA
00720 	BMI	OVFLOW
00730 	PSHS	D
00740 	LDD	#639
00750 	SUBD	,S++
00760 	BCC	D0
00770 	LDD	#0		PREVENT UNDERFLOW
00780 D0	RTS
00790 OVFLOW	LDD	#639		PREVENT OVERFLOW
00800 	RTS
00810 
00820 	END	START
